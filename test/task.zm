import std_choose_1of2_page;

import defs;
import stimuli;
import shuffle;
import output;
import test_page;
import picture_page;

Block task
{
    on_event:entry()
    {
        // Randomize test items table.
        if (shuffle_test_items() != OK) terminate;
    }

    Part instructions
    {
        on_event:entry()
        {
            pause_page.set_text( INSTRUCTION_TEXT, true);

            pause_page.action(this);
            control.button_box.enable_all_buttons(CONTINUE);
        }
    }

    Part instructions2
    {
        on_event:entry()
        {
            pause_page.set_text( INSTRUCTION_TEXT2, true);

            pause_page.action(this);
            control.button_box.enable_all_buttons(CONTINUE);
        }
    }


    Block practice
    {
        TestItem        item;   // current item

        on_event:entry()
        {
            num_cycles = prac_items.size;
        }


        on_event:exit()
        {
            test_page.cleanup();
        }


        Trial trial
        {
            on_event:entry()
            {
                item = prac_items[practice.cycle];

                // Prepare test page: progress bar and prompt enabled.
                test_page.setup(practice.num_cycles, true);
                test_page.action(this, item, reference_time, practice.cycle);
            }
        }

        Trial picture
        {
            on_event:entry()
            {
                picture_page.setup(practice.num_cycles, true);
                picture_page.action(this, item, reference_time, practice.cycle);
            }
        }
    }


    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }


    Part pause
    {
        on_event:entry()
        {
            pause_page.set_text(AFTER_PRACTICE_TEXT, true);
            pause_page.action(this);
            control.button_box.enable_all_buttons(CONTINUE);
        }
    }


    Block test
    {
        TestItem        item;   // current item

        on_event:entry()
        {
            num_cycles = test_items.size;
        }


        on_event:exit()
        {
            test_page.cleanup();

            // Save test output table as CSV file.
            test_output_save();
        }


        Trial trial
        {
            on_event:entry()
            {
                item = test_items[test.cycle];
            }

            on_event:exit()
            {
                TestOutput output;

                output.trial                 = test.cycle;
                output.identification        = item.identification;
                output.context_sentence      = item.context_sentence;
                output.target_sentence       = item.target_sentence;

                output.type                  = item.type;

                output.figure1_file_name     = item.figure1_file_name;
                output.figure2_file_name     = item.figure2_file_name;

                output.reading_time_sentence = test_page.segment_selection.reading_time;
                output.reaction_time         = picture_page.response.rt;
                output.figure_choice         = picture_page.response.choice;
                
                output.correct               = output.figure_choice == item.correct;

                // Add new row to test output table.
                test_output_append(output);
            }


            SubTrial presentation
            {
                on_event:entry()
                {
                    // Prepare test page: progress bar enabled, prompt disabled.
                    test_page.setup(test.num_cycles, false);
                    test_page.action(this, item, reference_time, test.cycle);
                }
            }

            SubTrial picture
            {
                on_event:entry()
                {
                    picture_page.setup(test.num_cycles, false);
                    picture_page.action(this, item, reference_time, test.cycle);
                }
            }


            TransientPart
            {
                on_event:entry()
                {
                    if(!at_a_pause_trial())
                        trial.leave();  // No question. Trial done.
                }

                bool at_a_pause_trial()
                {
                    return test.cycle == test.num_cycles/2;
                }
            }


            SubTrial
            {
                on_event:entry()
                {
                    pause_page.set_text( PAUSE_TEXT, true);
                    pause_page.action(this);
                }
            }
        }
    }

    Part
    {
        on_event:entry()
        {
            blank_page.action_auto(this, 500ms);
        }
    }
}


void action(Part caller)
{
    // Activate the task. When the task is done, the registered caller
    // (c.q. the main experiment object) will automatically be signalled.
    task.enter(caller);
}
